import org.gradle.api.internal.artifacts.dependencies.ProjectDependencyInternal

/**
 * Предназначен для проверки наличия SNAPSHOT зависимостей компонента.
 * При наличии данных зависимостей будет выброшено исключение IllegalStateException
 *
 * Использование:
 *      apply from: 'snapshot-dependencies.gradle'
 */

task checkComponentSnapshotDependencies {
    doLast {
        def isForceRelease = project.hasProperty("forceRelease") && project.getProperty("forceRelease") == "true"
        if (isForceRelease) {
            logger.lifecycle("Force release action. SKIPPED")
            return
        }

        Set snapshotPackages = []

        project.configurations.each { conf ->
            conf.allDependencies.findAll { dep -> !(dep instanceof ProjectDependencyInternal) }.each { dep ->
                    if (dep.version?.toUpperCase()?.endsWith('-SNAPSHOT') ||
                            dep.version?.matches('^.+(\\d{8}\\.\\d{6}\\-\\d+)$')) { //direct snapshot version
                        snapshotPackages.add("${dep.group}:${dep.name}:${dep.version}")
                    }
            }
        }

        if (!snapshotPackages.isEmpty()) {
            throw new IllegalStateException("You have the following SNAPSHOT dependencies:\r\n${snapshotPackages}")
        }
    }
}